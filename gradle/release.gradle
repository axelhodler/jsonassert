apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'io.codearte.nexus-staging'

String projectDescription = '''Spock extension that allows you to inject Collaborators into Subjects. Bases on Mockito's @InjectMocks functionality'''

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name "$project.name"
				packaging 'jar'
				description projectDescription
				url 'https://github.com/marcingrzejszczak/spock-subjects-collaborators-extension'

				scm {
					connection 'git@github.com:marcingrzejszczak/spock-subjects-collaborators-extension.git'
					developerConnection 'git@github.com:marcingrzejszczak/spock-subjects-collaborators-extension.git'
					url 'https://github.com/marcingrzejszczak/spock-subjects-collaborators-extension'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'marcingrzejszczak'
						name 'Marcin Grzejszczak'
						email 'marcin ATT grzejszczak DOTT pl'
					}
				}
			}
		}
	}
}

uploadArchives.dependsOn(check)
bintrayUpload.dependsOn(check)

task release(dependsOn:['uploadArchives', 'bintrayUpload']) {}

bintray {
	user = project.properties['bintrayUser']
	key = project.properties['bintrayKey']
	publications = ['code']
	pkg {
		repo = 'com-blogspot-toomuchcoding'
		name = 'spock-subjects-collaborators-extension'
		desc = projectDescription
		licenses = ['Apache-2.0']
		labels = ['spock']
	}
}

